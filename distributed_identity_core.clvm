(mod (corehash parent_info my_amount puzzle_reveal solution)
; 51 = CREATE_COIN
; 52 = ASSERT_CONSUMED
; 53 = ASSERT_MY_ID

; This is for the core

  (defmacro and ARGS
    (if ARGS
      (qq (if (unquote (f ARGS))
            (unquote (c and (r ARGS)))
            ()
      ))
    1)
  )

  (defmacro not (ARGS)
    (qq (if (unquote ARGS) 0 1))
  )

  ; takes a lisp tree and returns the hash of it, but allows for escape character 97 so i precomputed hash value can be used
  (defun sha256tree0 (TREE)
    (if (l TREE)
      (if (and (not (l (f TREE))) (= (f TREE) 97))
          (f (r TREE))
          (sha256 2 (sha256tree0 (f TREE)) (sha256tree0 (r TREE)))
      )
      (sha256 1 TREE)
    )
  )

  ; takes a lisp tree and returns the hash of it
  (defun sha256tree1 (TREE)
      (if (l TREE)
          (sha256 2 (sha256tree1 (f TREE)) (sha256tree1 (r TREE)))
          (sha256 1 TREE)
      )
  )

  (defmacro create_outputs (puzreveal solution)
      (qq ((c (unquote puzreveal) (unquote solution))))
  )

  (defun create_fullpuzhash (innerpuzzlehash corehash)
    (sha256tree0 (qq (r (c (q (unquote innerpuzzlehash)) ((c (q (97 (unquote corehash))) (a)))))))
  )

  ; assembles information from the solution to create our own full ID including asserting our parent is a coloured coin
  (defmacro create_my_ID ((parent_parent parent_innerpuz parent_amount) my_innerpuz my_amount corehash)
    (qq (sha256 (sha256 (unquote parent_parent) (create_fullpuzhash (unquote parent_innerpuz) (unquote corehash)) (unquote parent_amount)) (create_fullpuzhash (unquote my_innerpuz) (unquote corehash)) (unquote my_amount)))
  )

  ; asserts that the information in the solution about ourselves is correct, and that our parent is a coloured coin
  (defmacro assert_my_id (my_ID)
    (qq (c (q 53) (c (unquote my_ID) (q ()))))
  )

  (defun check_my_amount_and_ID ((parent_parent parent_innerpuz parent_amount) my_innerpuz my_amount corehash)
    (if (> my_amount 0)
      (assert_my_id (create_my_ID (parent_parent parent_innerpuz parent_amount) my_innerpuz my_amount corehash))
      (x)
    )
  )

  (defun check_outputs_value (outputs_loop flag)
    (if outputs_loop
      (if (= (f (f outputs_loop)) (q 51))
        (if (> (f (r (r (f outputs_loop)))) 0)
          (if flag
            (x)
            (check_outputs_value (r outputs_loop) 1)
          )
          (check_outputs_value (r outputs_loop) flag)
        )
        (check_outputs_value (r outputs_loop) flag)
      )
      1
    )
  )

  (defun check_id_and_check_outputs (corehash parent_info my_amount puzzle_reveal outputs)
    (if (check_outputs_value outputs 0)
      (c (check_my_amount_and_ID parent_info puzzle_reveal my_amount corehash) outputs)
      (x)
    )
  )

  ;main
  ;(f (f (create_outputs puzzle_reveal solution)))
  ;(list corehash parent_info my_amount puzzle_reveal (create_outputs puzzle_reveal solution))
  ;(check_outputs_value (create_outputs puzzle_reveal solution) 0)
  ;(check_my_amount_and_ID parent_info puzzle_reveal my_amount corehash)
  (check_id_and_check_outputs corehash parent_info my_amount puzzle_reveal (create_outputs puzzle_reveal solution))

)
